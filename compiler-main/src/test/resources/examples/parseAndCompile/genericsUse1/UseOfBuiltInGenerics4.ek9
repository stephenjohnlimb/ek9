#!ek9
<?-
  See badParametersForInference.ek9 for lots of bad examples the compiler detects.
-?>
defines module simple.generics.use.four

  defines class

    <?-
      Outline of a generic 'thing'.
    -?>
    //Symbol 1
    @Resolved: SYMBOL_DEFINITION: TEMPLATE_TYPE: "GenericThing"
    GenericThing of type T
      item as T?

      default GenericThing()

      GenericThing()
        -> arg as T
        this.item = arg

      getItem()
        <- rtn as T: item

      default operator ?

    <?-
      Another generic thing but with two parameterizing types.
      Also examples of different ways to return values, explicit types,
      but also via the <- mechanism.
    -?>
    //Symbol 2
    @Resolved: SYMBOL_DEFINITION: TEMPLATE_TYPE: "GenericMapThing"
    GenericMapThing of type (K, V)
      k as K?
      v as V?

      default GenericMapThing()

      GenericMapThing()
        ->
          key as K
          value as V
        this.k = key
        this.v = value

      key()
        <- rtn as K: k
      value()
        <- rtn as V: v

      default operator ?

  defines function

    //Symbol 3
    @Resolved: SYMBOL_DEFINITION: FUNCTION: "FunctionWithExplicitGenericConstruction"
    FunctionWithExplicitGenericConstruction()

      //So these will get created in polymorphic parameterised form
      //Symbol 4
      @Resolved: SYMBOL_DEFINITION: TYPE: "GenericThing of (DateTime)"
      thingOfDateTime <- GenericThing() of DateTime
      assert ~thingOfDateTime?


      //Symbol 5
      @Resolved: SYMBOL_DEFINITION: TYPE: "GenericThing of (Duration)"
      thingOfDuration <- GenericThing() of Duration
      assert ~thingOfDuration?

      //But for now in early compiler phases focus on this
      //Looks like inferred - but really is explicit
      //Symbol 6
      @Resolved: SYMBOL_DEFINITION: TYPE: "GenericThing of (Date)"
      thingOfDate <- GenericThing() of Date
      assert ~thingOfDate?

      //Now make a float version
      //Symbol 7
      @Resolved: SYMBOL_DEFINITION: TYPE: "GenericThing of (Float)"
      thingOfFloat <- GenericThing() of Float
      assert ~thingOfFloat?

      //Symbol 8 and 9
      @Resolved: SYMBOL_DEFINITION: TYPE: "GenericMapThing of (Integer, GenericThing of (Millisecond))"
      thingOfMap <- GenericMapThing() of (Integer, GenericThing of Millisecond)
      assert ~thingOfMap?

      //Now because we use BespokeClass before we have parsed it in the first phase
      //This will not get resolved - compiler accepts this in the first phase but second pass
      //Both BespokeClass and GenericThing as both known and so should then fully resolve.
      //Symbol 10
      @NotResolved: SYMBOL_DEFINITION: TYPE: "GenericThing of (BespokeClass)"
      thingOfBespokeClass <- GenericThing() of BespokeClass
      assert ~thingOfBespokeClass?

      @Resolved: EXPLICIT_TYPE_SYMBOL_DEFINITION: TYPE: "GenericThing of (BespokeClass)"
      nowResolvedAtThisPhase <- GenericThing() of BespokeClass
      assert nowResolvedAtThisPhase?

  //Now add in a class to be used with the GenericThing but 'after' its use above
  //For the first definition phase this won't be resolved above and create a GenericThing of BespokeClass
  //But in the next definition of explicit types phase it will, because the first phase will have
  //found BespokeClass - hence on the second pass the above will get fully resolved.
  defines class

    //Symbol 11
    @Resolved: SYMBOL_DEFINITION: TYPE: "BespokeClass"
    BespokeClass
      n <- "Steve"

      name()
        <- rtn as String: n

//EOF