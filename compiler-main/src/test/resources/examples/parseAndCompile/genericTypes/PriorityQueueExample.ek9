#!ek9

<?-
  Little test drive of the Priority Queue generic class that is built in to EK9.
-?>
defines module com.customer.priority.example

  defines type
    List of String
    
  defines program
    PQApplication
      underTest <- SimpleQueueCheck()
      underTest.testMethod()
      
  defines class
  
    SimpleQueueCheck
    
      checkUsingList()
        people <- ["Steve", "Mark", "Matthew" ]

        //withSize and useComparator - creates a new queue from the existing (leaving the existing as is)
        q1 <- PriorityQueue() of String
        q2 <- q1.withSize(5)
        
        q2 += people
        assert q2?

      testMethod()

        //Make a priority queue, but get it to create a new one with specific size.
        //Note the use of parenthesis to enable 'dot' object access.
        q1 <- (PriorityQueue() of String).withSize(5)
        
        people <- ["Steve", "Mary", "Mungo", "Midge", "Cain"]
        q1 += people
                
        showContents(q1)
        
        q1 += "Jimbo"
        showContents(q1)        
              
      private showContents()
        -> q as PriorityQueue of String
        assert q?
        stdout <- Stdout()
        iter <- q.list().iterator()
        stdout.println("Queue List is [")
        while iter?
          stdout.println("PQ " + iter.next())          
        stdout.println("]")
//EOF