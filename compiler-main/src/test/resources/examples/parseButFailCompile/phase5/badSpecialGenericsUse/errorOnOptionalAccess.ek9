#!ek9
<?-
  As with Result, the Optional generic type is treated specially by the compiler and forces
  and 'is-set' '?' operation before a safe call to 'get()' can be made.
  This sample code also shows several of the other methods and operations on the 'Optional' class.
  There is also some examples of options around creating empty Optionals and passing Suppliers/Producers
  around.
  So there is a mix of 'happy working examples' and also so examples of compiler errors.
-?>
defines module error.on.optional.access

  defines function

    <?-
      Improved compiler, so that now simple input argument can be used in inference.
    -?>
    getInferredFromArgOptional()
      -> arg0 as String
      <- rtn <- Optional(arg0)

    <?-
      Now this will return an Optional that is 'empty'.
      Similar to the Java code 'Optional.empty()'.
    -?>
    getExplicitOptional()
      <- rtn <- Optional() of String

    tetExplicitOptional()
      willBeEmpty <- getExplicitOptional()
      //A few assertions all proving the same thing.
      //'empty' is a normal operator and is implemented by the Optional class.
      assert willBeEmpty is empty
      assert not willBeEmpty?
      assert not willBeEmpty.iterator().hasNext()

    getViaSimpleConstructor()
      <- rtn <- AClass()

    testViaSimpleConstructor()
      aClass <- getViaSimpleConstructor()
      assert aClass.getProperty()?

      //Could have just done this
      assert getViaSimpleConstructor().getProperty()?

    getInferredOptional1()
      <- rtn <- Optional("")

    testInferredOptional1()
      o <- getInferredOptional1()
      if o?
        assert length o.get() == 0

    getInferredOptional2()
      <- rtn <- Optional(String())

    testInferredOptional2()
      o <- getInferredOptional2()
      //Or more readably
      assert o is empty

    testGenericsDeclarationInPreflow()
      //It's a bit pointless, because obviously the Optional is set.
      //But having had a parsing bug, that separated the ("Steve") as an expression
      //I want this for regression test.
      if o <- Optional("Steve")
        val <- o.get()
        assert val?

      //The nice thing about declaring 'in' the 'if' statement is that o is not available outside that scope
      //So to prove that I'll declare 'another o'. I'll even use a different type.
      o <- 'O'
      assert o?


    testValidUseDueToChecks1()

      o <- Optional("Steve")

      if iter <- o.iterator() then iter?
        okValue <- iter.next()
        assert okValue.lowerCase()?

      //Now if you want the value of whats in the Optional or a default
      //here is an EK9 idiomatic way of accomplishing that via an iterator.
      iter <- o.iterator()
      value <- true and iter.hasNext() <- iter.next() else String()
      assert value?

      //But the best EK9 idiomatic way is like this
      checkValue <- o? <- o.get() else String()
      assert checkValue?

      //This is a similar capability as above but built into Optional.
      assuredValue <- o.getOrDefault("Stephen")
      assert assuredValue?


  defines class

    <?-
      Used as an example for determining if return type can be inferred.
    -?>
    AClass
      prop1 <- "Steve"

      getProperty()
        <- rtn as String: prop1

      default operator ?

  defines function

    <?-
      Use of a generic function - but for this not sure it's worth it.
    -?>
    EmptyOptional() of type T as open
      <- rtn as Optional of T: Optional(T())

    <?-
      Example of a higher order function using the above generic function.
      Again not really worth the effort.
    -?>
    getProducerOfEmptyOptionalOfString()
      <- rtn as EmptyOptional of String: () is EmptyOptional of String as function

    <?-
      Example of using the higher order function.
    -?>
    testCreateEmptyOptional1()
      supplier <- getProducerOfEmptyOptionalOfString()

      emptyOptionalOfString <- supplier()
      assert emptyOptionalOfString is empty

    <?-
      Or just use this simple approach.
    -?>
    testCreateEmptyOptional2()
      emptyOptionalOfString <- Optional() of String
      assert emptyOptionalOfString is empty

    <?-
      Little need for any generic functions, just use this approach.
      You can concoct all sorts of combinations with higher order and
      Producers and Suppliers if you wish - but try and keep it simple.
      Only use the passing of functions around if you really need it.
      This will produce an 'empty' optional.
    -?>
    simplerProducerOfEmptyOptionalOfString()
      <- rtn <- Optional() of String

    testSimplerProducerOfOptionalOfString()
      emptyOptionalOfString <- simplerProducerOfEmptyOptionalOfString()
      assert emptyOptionalOfString is empty

    <?-
      But you can use this 'constructor' based approach, which is slightly simpler.
      This will produce an Optional that has a String OK value - but this unset.
    -?>
    evenSimplerProducerOfEmptyOptionalOfString()
      <- rtn <- Optional(String())

    testEvenSimplerProducerOfOptionalOfString()
      emptyOptionalOfString <- evenSimplerProducerOfEmptyOptionalOfString()
      assert emptyOptionalOfString is empty

    usesAProducerOfOptionalOfString()
      -> producer as Producer of Optional of String
      optionalOfString <- producer()
      assert optionalOfString is empty

    testProducerUse()
      usesAProducerOfOptionalOfString(simplerProducerOfEmptyOptionalOfString)

    <?-
      You can also do the pure form of the above with 'Suppliers'.
    -?>
    simplerSupplierOfEmptyOptionalOfString() as pure
      <- rtn <- Optional() of String

    testSimplerSupplierOfOptionalOfString() as pure
      emptyOptionalOfString <- simplerSupplierOfEmptyOptionalOfString()
      assert emptyOptionalOfString is empty

    usesASupplierOfOptionalOfString() as pure
      -> supplier as Supplier of Optional of String
      optionalOfString <- supplier()
      assert optionalOfString is empty

    testSupplierUse() as pure
      usesASupplierOfOptionalOfString(simplerSupplierOfEmptyOptionalOfString)

    //The above examples, demonstrate what is possible - not always desirable as more complex.

    <?-
      Just used with whenPresent on Optional - none-pure form.
    -?>
    someAcceptor()
      -> arg0 as String
      assert arg0?

    <?-
      Just used with whenPresent on Optional - pure form.
    -?>
    someConsumer() as pure
      -> arg0 as String
      assert arg0?

    <?-
      Probably the most common coding pattern for safe access to the value in an Optional.
      This is the easiest to understand for most developers.
      Does it hold a value? if yes then access the value and do something with it.
    -?>
    testSimpleValidIfCheck1()
      o <- Optional("Steve")

      if o?
        value <- o.get()
        assert value?

    testSimpleValidIfCheck2()
      o <- Optional("Steve")

      //Just a different way to address the is-set operator
      if o.?()
        value <- o.get()
        assert value?

    <?-
      Demonstrates that calls to 'whenPresent' can be made without any other checks like 'o?'.
      Now this form will be most common for developers with a more 'functional' background.
      Here it uses the Consumer (which is pure in nature).
    -?>
    testUsingConsumer() as pure
      o <- Optional("Steve")
      o.whenPresent(someConsumer)

    <?-
      Demonstrates that calls to 'whenPresent' can be made without any other checks like 'o?'.
      This too will be more common for developers with a functional background.
      In this case it uses the 'Acceptor' which is non-pure in nature - i.e. is capable of mutations.
    -?>
    testUsingAcceptor()
      o <- Optional("Steve")
      o.whenPresent(someAcceptor)

    <?-
      Demonstrates the easiest 'idiomatic' way to get the value out of an Optional
      or default it if it not present.
      This is quite a nice patter as it can be used in a 'pure' context if required.
    -?>
    testUsingTernary() as pure
      o <- Optional("Steve")

      value <- o? <- o.get() else String()
      assert value?

    <?-
      Demonstrates some additional methods on Optional.
    -?>
    testGeneralMethods()
      o <- Optional("Steve")

      //Just check if it holds a value
      assert o is not empty

      //Weird, but check it it has a specific value
      //Again quite nice because 'contains' is pure.
      assert o contains "Steve"

      //If you have an Optional and you want a new optional of the same parameterised type
      //that has no value here is a quick way to accomplish that.
      emptyOptional <- o.asEmpty()
      assert emptyOptional is empty

      assert o != emptyOptional

      alsoEmptyOptional <- o.asEmpty()

      assert alsoEmptyOptional == emptyOptional

      //You can cat an optional, get the value out and then collect into an Optional via pipe
      //A bit pointless here, but useful in some situations, especially with other pipe operations
      //like head, skip, tail etc.
      alsoOptionalOfSteve <- cat o | collect as Optional of String
      assert alsoOptionalOfSteve contains "Steve"

      //You can also copy over the contents (if present) of an Optional
      newO <- Optional(String())
      newO :=: alsoOptionalOfSteve
      assert newO contains "Steve"

    testUsingIteratorAndTernary()
      o <- Optional("Steve")
      iter <- o.iterator()

      value <- iter.hasNext() <- iter.next() else String()
      assert value?

    testUsingIteratorAndIf()
      o <- Optional("Steve")

      possibleValue <- String()

      if iter <- o.iterator() then iter.hasNext()
        possibleValue: iter.next()

      assert possibleValue?

    <?-
      Demonstrates how only the 'true' part of the ternary is protected.
    -?>
    testInvalidUsingTernary1()
      o <- Optional("Steve")

      @Error: PRE_IR_CHECKS: UNSAFE_METHOD_ACCESS
      value <- o? <- "Something" else o.get()
      assert value?

    <?-
      Again demonstrates that even adding a 'not' in the control logic adds too much
      complexity for the EK9 developer/compiler. The idea it to ensure tht the logic has to be
      simple. This aids understanding and ease of comprehension by the EK9 developer.
      i.e. Avoid complex logic keep it stupid simple/keep it simple stupid (KISS).
    -?>
    testInvalidUsingTernary2()
      o <- Optional("Steve")

      @Error: PRE_IR_CHECKS: UNSAFE_METHOD_ACCESS
      value <- not o? <- "Something" else o.get()
      assert value?

    <?-
      Demonstrates how direct access to 'get()' will be detected if a call to 'o?' is not made first.
    -?>
    testInvalidGetAccess1()
      o <- Optional("Steve")

      @Error: PRE_IR_CHECKS: UNSAFE_METHOD_ACCESS
      value <- o.get()
      assert value?

    <?-
      While you may think this code is OK, EK9 is quite strict on not allowing access
      unless it is really obvious to both the EK9 developer and the compiler.
      So just making a call to Optional '?()' is not enough, it has to be in the context of
      a protective block, if an 'if' or a 'ternary'.
    -?>
    testInvalidGetAccess2()
      o <- Optional("Steve")

      hasValue <- o?
      assert hasValue

      if hasValue
        @Error: PRE_IR_CHECKS: UNSAFE_METHOD_ACCESS
        value <- o.get()
        assert value?

      @Error: PRE_IR_CHECKS: UNSAFE_METHOD_ACCESS
      alsoValue <- o.get()
      assert alsoValue?

    <?-
      Really a check of the use of the iterator from the Optional.
    -?>
    testInvalidOptionalIterator1()
      o <- Optional("Steve")

      //A valid use first
      if iter <- o.iterator() then iter?
        value <- iter.next()
        assert value?

      //Can also be used with while
      while iter <- o.iterator() then iter?
        value <- iter.next()
        assert value?

      //But if accessed directly i.e. not is a checked scope - error
      iter <- o.iterator()

      @Error: PRE_IR_CHECKS: UNSAFE_METHOD_ACCESS
      value <- iter.next()
      assert value?

    testCheckedButThenReAssigned()
      o <- Optional("Steve")

      if o?
        val <- o.get()
        //Now it is no longer safe! We do not allow mutation of 'o' in a safe block.
        @Error: PRE_IR_CHECKS: NO_REASSIGNMENT_WITHIN_SAFE_ACCESS
        o: Optional("Stephen")
        val: o.get()
        assert val?

      //But you can reassign outside of the safe block.
      o: Optional("Stephen")
      assert o?
//EOF