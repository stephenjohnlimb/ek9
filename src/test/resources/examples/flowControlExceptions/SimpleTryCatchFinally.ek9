#!ek9
defines module com.customer.exceptions

  defines type

    BigCat
      Lion
      Tiger
      WildCat
      Leopard
      Lynx

  defines class

    AnException extends Exception
      supportingInformation <- String()

      AnException()
        ->
          primaryReason as String
          supportingInformation as String
          exitCode as Integer
        super(primaryReason, exitCode)
        this.supportingInformation :=: supportingInformation

      supportingInformation() as pure
        <- rtn as String: supportingInformation

      override operator $ as pure
        <- rtn as String: `${reason()} ${supportingInformation()} exit code ${exitCode()}`

    OtherException extends Exception
      retryAfter <- DateTime()

      OtherException()
        ->
          reason as String
          retryAfter as DateTime
        super(reason)
        this.retryAfter :=: retryAfter

      retryAfter() as pure
        <- rtn as DateTime: retryAfter

      override operator $ as pure
        <- rtn as String: reason() + " retry after " + $retryAfter()

    ExceptionExample
      clock as Clock?
      deferProcessingUntilAfter <- DateTime()

      default private ExceptionExample()

      ExceptionExample()
        -> clock as Clock
        this.clock: clock

      checkExceptionHandling()
        -> aCat as BigCat
        <- didProcess <- false

        stdout <- Stdout()
        stderr <- Stderr()

        try
          if deferProcessing()
            stdout.println(`Deferred until after ${deferProcessingUntilAfter} ${aCat} not processed`)
          else
            triggerResult <- triggerPossibleException(aCat)
            stdout.println(triggerResult)
            didProcess: true
        catch
          -> ex as Exception
          errorMessage <- handleException(ex)
          stderr.println(errorMessage)
        finally
          stdout.println(`Finished checking ${aCat}`)

      triggerPossibleException()
        -> aCat as BigCat
        <- rtn as String: String()

        switch aCat
          case BigCat.Lion
            throw Exception($aCat, 1)
          case BigCat.Tiger
            throw AnException("Too dangerous", $aCat, 2)
          case BigCat.WildCat
            rtn: `Success with a little next gen Wild Cat`
          case BigCat.Leopard
            throw OtherException($aCat, clock.dateTime() + PT2H)
          case BigCat.Lynx
            rtn: `Success with a little Lynx`
          default
            rtn: `Success with and unknown ${aCat}`

      deferProcessing()
        <- rtn as Boolean: deferProcessingUntilAfter? <- deferProcessingUntilAfter > clock.dateTime() else false

      private handleException() as dispatcher
        -> ex as Exception
        <- rtn as String: $ex

      private handleException()
        -> ex as AnException
        <- rtn as String: $ex
        if ex.exitCode()?
          tidyUpReadyForProgramExit()

      private handleException()
        -> ex as OtherException
        <- rtn as String: $ex
        this.deferProcessingUntilAfter: ex.retryAfter()

      private tidyUpReadyForProgramExit()
        Stdout().println("Would tidy up any state ready for program exit")

    FileExceptionExample

      demonstrateFileNotFound()
        stdout <- Stdout()
        stderr <- Stderr()

        try
          -> input <- TextFile("MightNotExist.txt").input()
          cat input > stdout
        //rather than use catch 'handle' can be used
        handle
          -> ex as Exception
          stderr.println($ex)
        finally
          stdout.println("Automatically closed file if opened")

      demonstrateNamedFileNotFound()
        -> fileName as String

        mainResults <- try
          -> input1 <- TextFile(fileName).input()
          <- rtn as List of String: cat input1 | collect as List of String

        //Let the exceptions fly back - don't handle in here.
        Stdout().println(`Main Results ${mainResults}]`)

      demonstrateFilesNotFound()

        mainResults <- try
          ->
            input1 <- TextFile("MightNotExist.txt").input()
            input2 <- TextFile("AlsoMightNotExist.txt").input()
          <-
            rtn as List of String: cat input1, input2 | collect as List of String

        //Let the exceptions fly back - don't handle in here.
        Stdout().println(`Main Results ${mainResults}]`)

  defines function
    testFunction()
      -> arg0 as String
      <- rtn as String: String()

  defines program

    <?-
      The logic here is that if the 'testFunction' returns something that means that 'someVar' is
      'unset' then the 'guard' is triggered and the try block never executes - it's like putting
      an 'if someVar?' around the try, catch, finally block.
    -?>
    TryWithGuard()
      someVar as String?
      try someVar ?= testFunction("Steve")
        example2 <- FileExceptionExample()
        example2.demonstrateNamedFileNotFound(someVar)
      catch
        -> ex as Exception
        Stderr().println("TryWithGuard: " + $ex)

    <?-
      This is similar to the above, but is just an assignment, so that 'someVar' can still be 'unset'.
    -?>
    TryWithAssignment()
      someVar as String?
      try someVar: testFunction("Steve")
        example2 <- FileExceptionExample()
        example2.demonstrateNamedFileNotFound(someVar)
      catch
        -> ex as Exception
        Stderr().println("TryWithAssignment: " + $ex)

    <?-
      Finally in this case, the variable is declared only in the scope of try/catch/finally and may or may
      not remain 'unset'. So the try/catch/finally block does execute even if 'someVar' is 'unset'.
      'someVar' is visible in the try, catch, finally block, but then goes out of scope.
    -?>
    TryWithDeclaration()

      try someVar <- testFunction("Steve")
        example2 <- FileExceptionExample()
        example2.demonstrateNamedFileNotFound(someVar)
      catch
        -> ex as Exception
        Stderr().println(`TryWithDeclaration: ${ex}`)

      //This is a 'different' 'someVar' - the previous one is now out of scope.
      someVar <- false
      assert someVar?

    TryCatchExample()
      stdout <- Stdout()
      stderr <- Stderr()

      //Rather than use SystemClock - simulate one so that date time can be altered.
      simulatedClock <- () with trait of Clock as class
        currentDateTime as DateTime: 1971-02-01T12:00:00Z
        override dateTime() as pure
          <- rtn as DateTime: currentDateTime

        setCurrentDateTime()
          -> newDateTime as DateTime
          this.currentDateTime = newDateTime

      //use the simulated clock
      example1 <- ExceptionExample(simulatedClock)

      for cat in BigCat
        if example1.checkExceptionHandling(cat)
          stdout.println(`Processing of ${cat} was completed`)
        else
          stderr.println(`Processing of ${cat} was NOT completed`)

      //just try Lynx again
      assert ~ example1.checkExceptionHandling(BigCat.Lynx)

      //alter the time just passed the retry after time.
      simulatedClock.setCurrentDateTime(simulatedClock.dateTime() + PT2H1M)
      //Now it should be processed.
      assert example1.checkExceptionHandling(BigCat.Lynx)

      example2 <- FileExceptionExample()
      example2.demonstrateFileNotFound()

      try
        example2.demonstrateFilesNotFound()
      catch
        -> ex as Exception
        Stderr().println(`TryCatchExample: ${ex}`)
//EOF