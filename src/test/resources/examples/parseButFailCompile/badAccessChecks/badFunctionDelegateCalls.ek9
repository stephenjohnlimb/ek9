#!ek9
<?-
  Test for resolving and calling function delegates - which look like methods.
  This includes quite a few of different error conditions.
-?>
defines module bad.functiondelegates.examples

  defines function
    SomeFunction()
      <- rtn <- true

    AnotherFunction()
      -> arg0 as String
      <- rtn as Boolean: arg0?

    HigherFunctionOne()
      <- rtn as SomeFunction: SomeFunction

    HigherFunctionTwo()
      <- rtn as AnotherFunction: AnotherFunction

  defines class
    C1

      testCanCallFunctionDirectly()
        result <- SomeFunction()
        assert result?

      testCallToNonDelegate()
        nonDelegate <- 1
        assert nonDelegate?

        //Check to make sure this can be detected, it's just an integer
        @Error: FULL_RESOLUTION: NOT_A_FUNCTION_DELEGATE
        notValidResult <- nonDelegate()
        @Error: FULL_RESOLUTION: TYPE_NOT_RESOLVED
        assert notValidResult?

      testDelegateVariableToFunction()
        delegate1A <- SomeFunction
        assert delegate1A?

        result1A <- delegate1A()
        assert result1A?

      testHigherFunctionOneA()
        delegate1B <- HigherFunctionOne()
        assert delegate1B?

        //So if that worked then I should be able to 'call' that delegate1A - because it will be SomeFunction
        result1B <- delegate1B()
        assert result1B?

      testHigherFunctionOneAWithInvalidParameters()
        delegate1C <- HigherFunctionOne()
        assert delegate1C?

        //So if that worked then I should be able to 'call' that delegate1A - because it will be SomeFunction
        //But use invalid parameters and check we get an error.
        @Error: FULL_RESOLUTION: FUNCTION_PARAMETER_MISMATCH
        result1C <- delegate1C(false, "Steve")
        @Error: FULL_RESOLUTION: TYPE_NOT_RESOLVED
        assert result1C?

      testHigherFunctionTwoA()
        delegate2A <- HigherFunctionTwo()
        assert delegate2A?

        //So if that worked then I should be able to 'call' that delegate2A - because it will be AnotherFunction
        result2A <- delegate2A("Steve")
        assert result2A?

        //But this will fail because incorrect parameters.
        @Error: FULL_RESOLUTION: FUNCTION_PARAMETER_MISMATCH
        result2B <- delegate2A(true, "Steve")
        @Error: FULL_RESOLUTION: TYPE_NOT_RESOLVED
        assert result2B?

//EOF